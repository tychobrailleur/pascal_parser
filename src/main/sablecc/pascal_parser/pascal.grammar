/*

   Copyright (C) 2016  Sébastien Le Callonnec

   This program is free software; you can redistribute it and/or modify
   it under the terms of the GNU General Public License as published by
   the Free Software Foundation; either version 3 of the License, or
   (at your option) any later version.

   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU General Public License for more details.

   You should have received a copy of the GNU General Public License
   along with this program; if not, write to the Free Software Foundation,
   Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA

*/

Package pascal_parser;


Helpers

  any_character = [0x0 .. 0xfffff];
  lf = 10;
  cr = 13;
  letter = ['a'..'z'] | ['A'..'Z'];
  digit = ['0'..'9'];
  letter_or_underscore = letter | '_';

  line_terminator = lf | cr | cr lf;
  left_brace = '{';
  right_brace = '}';
  single_quote = ''';

  float = digit (digit)* '.' digit (digit)* (('E' | 'e') ('+' | '-')? digit (digit)*)?;



Tokens

  begin = 'begin';
  end = 'end';
  program = 'program';
  const = 'const';
  type = 'type';
  goto = 'goto';
  label = 'label';
  case = 'case';
  of = 'of';
  if = 'if';
  then = 'then';
  else = 'else';
  repeat = 'repeat';
  for = 'for';
  to = 'to';
  downto = 'downto';
  do = 'do';
  not = 'not';
  until = 'until';
  var = 'var';
  while = 'while';
  procedure = 'procedure';
  forward = 'forward';
  packed = 'packed';
  function = 'function';

  false = 'false';
  true = 'true';

  // types
  boolean = 'boolean';
  file = 'file';
  record = 'record';
  integer = 'integer';
  real = 'real';
  array = 'array';
  char = 'char';
  byte = 'byte';

  dot = '.';
  semicolon = ';';
  colon = ':';
  comma = ',';
  range_op = '.' '.';
  left_square_bracket = '[';
  right_square_bracket = ']';
  left_paren = '(';
  right_paren = ')';
  assign_op = ':=';
  neq_diamond = '<>';
  lt = '<';
  gt = '>';
  leq = '<=';
  geq = '>=';
  neq = '!=';
  equal = '=';
  caret = '^';

  plus = '+';
  minus = '-';
  times = '*';
  divided = '/';
  div = 'div';
  mod = 'mod';
  or = 'or';
  and = 'and';

  comment = left_brace [any_character-right_brace]* right_brace;
  blank = ' ' | '\t' | line_terminator;

  identifier = letter_or_underscore (letter_or_underscore | digit)*;
  number = digit+;
  float = float;

  // String literal.  Single-quotes are escaped by doubling them.
  string_literal = single_quote ([single_quote + single_quote] | [any_character - [''' + [cr + lf]]])* single_quote;



Ignored Tokens

  comment,
  blank;



Productions

  pascal_program { -> pascal_program } =
      program_header
      program_decl
      program_body
      dot { -> New pascal_program(program_header, [program_decl.statement], [program_body.statement]) };

  program_header { -> program_header } =
      program [name]:identifier semicolon { -> New program_header(name) };

  program_decl { -> statement* } =
      program_decl_item* { -> [program_decl_item.statement] };

  program_decl_item { -> statement* } =
      {label} label_decl_block { -> [label_decl_block.statement] } |
      {const} const_decl_block { -> [const_decl_block.statement] } |
      {var} var_decl_block { -> [var_decl_block.statement] } |
      {type} type_decl_block { -> [type_decl_block.statement] } |
      {proc} proc_decl { -> [proc_decl.statement] } |
      {func} func_decl { -> [func_decl.statement] };

  label_decl_block { -> statement* } =
      {single} label_decl semicolon { -> [label_decl.statement] };

  const_decl_block { -> statement* } =
      const const_decl_list { -> [const_decl_list.statement] };

  const_decl_list { -> statement* } =
      const_decl+ { -> [const_decl.statement] };

  const_decl { -> statement } =
      [name]:identifier equal expression semicolon { -> New statement.const(name, expression.expression) };

  var_decl_block { -> statement* } =
      var var_decl_list semicolon { -> [var_decl_list.statement] };

  var_decl_list { -> statement* } =
      {single} var_decl { -> [var_decl.statement] } |
      {multi} var_decl_list semicolon var_decl { -> [var_decl_list.statement, var_decl.statement] };

  var_decl { -> statement } =
      [vars]:identifier_list colon generic_type { -> New statement.var([vars.identifier], generic_type.typedef, Null) };

  type_decl_block { -> statement* } =
      type type_decl_list { -> [type_decl_list.statement] };

  type_decl_list { -> statement* } =
      type_decl+ { -> [type_decl.statement] };

  type_decl { -> statement } =
      [name]:identifier_list equal [type]:generic_type semicolon { -> New statement.type_decl([name.identifier], type.typedef) };

  proc_decl { -> statement } =
      procedure [proc_name]:identifier proc_param_decl? [end_head]:semicolon
      program_decl_item*
      proc_decl_body [end_proc]:semicolon
      { -> New statement.proc_decl(proc_name, [proc_param_decl.statement], [proc_decl_body.statement]) };

  proc_decl_body { -> statement* } =
      {fwd} forward { -> [] } |
      {forward} statement_block { -> [statement_block.statement] };

  proc_param_decl { -> statement* } =
      {args} left_paren proc_arg_list right_paren { -> [proc_arg_list.statement] };

  proc_arg_list { -> statement* } =
      {simple} var? var_decl { -> [var_decl.statement] } |
      {multi} proc_arg_list semicolon var_decl { -> [proc_arg_list.statement, var_decl.statement] };

  func_decl { -> statement } =
      function [name]:identifier proc_param_decl? colon [type]:simple_type [end_head]:semicolon
      program_decl_item*
      proc_decl_body [end_func]:semicolon
      { -> New statement.func_decl(name, type.typedef, [proc_param_decl.statement], [proc_decl_body.statement]) };

  identifier_list { -> identifier* } =
      {single} identifier { -> [identifier] } |
      {multi} identifier_list comma identifier { -> [identifier_list.identifier, identifier] };

  program_body { -> statement* } =
      statement_block { -> [statement_block.statement] };

  label_decl { -> statement } =
      label label_list { -> New statement.label([label_list.number]) };

  label_list { -> number* } =
      {single} number { -> [number] } |
      {multiple} label_list comma number { -> [label_list.number, number] };

  statement_block { -> statement } =
      begin statements end { -> New statement.block([statements.statement]) };

  statements { -> statement* } =
      {single} statement { -> [statement.statement] } |
      {multi} [list]:statements semicolon [stmt]:statement { -> [list.statement, stmt.statement] };

  statement { -> statement? } =
      {without_stmt} statement_without_trailing_stmt { -> statement_without_trailing_stmt.statement } |
      {if} if [cond]:expression then statement { -> New statement.if(cond.expression, [statement.statement], []) } |
      {if_else} if [cond]:expression then [then_branch]:stmt_no_short_if else [else_branch]:statement
                         { -> New statement.if(cond.expression, [then_branch.statement], [else_branch.statement]) } |
      {label} number colon statement { -> New statement.labelled(number, [statement.statement]) } |
      {while} while_stmt { -> while_stmt.statement } |
      {for} for_loop_stmt { -> for_loop_stmt.statement };

  stmt_no_short_if { -> statement? } =
      {without_stmt} statement_without_trailing_stmt { -> statement_without_trailing_stmt.statement } |
      {if} if [cond]:expression then [then_branch]:stmt_no_short_if else [else_branch]:stmt_no_short_if
                         { -> New statement.if(cond.expression, [then_branch.statement], [else_branch.statement]) } |
      {label} number colon stmt_no_short_if { -> New statement.labelled(number, [stmt_no_short_if.statement]) } |
      {while} while expression do stmt_no_short_if { -> New statement.while(expression.expression, [stmt_no_short_if.statement]) } |
      {for} for_loop_stmt_no_short_if { -> for_loop_stmt_no_short_if.statement };

  statement_without_trailing_stmt { -> statement? } =
      {group} statement_block { -> statement_block.statement } |
      {label} label_decl { -> label_decl.statement } |
      {goto} goto_stmt { -> goto_stmt.statement } |
      {assign} assignment { -> assignment.statement } |
      {proc_call} procedure_call { -> procedure_call.statement } |
      {repeat} repeat_stmt { -> repeat_stmt.statement } |
      {empty} { -> Null };

  goto_stmt { -> statement } =
      goto number { -> New statement.goto(number) };

  repeat_stmt { -> statement } =
      repeat statements until expression { -> New statement.repeat(expression.expression, [statements.statement]) };

  while_stmt { -> statement } =
      while expression do statement { -> New statement.while(expression.expression, [statement.statement]) };

  for_loop_stmt { -> statement } =
      {up} for assignment to expression do statement { -> New statement.for_up(assignment.statement, expression.expression, [statement.statement]) } |
      {down} for assignment downto expression do statement { -> New statement.for_down(assignment.statement, expression.expression, [statement.statement]) };

  for_loop_stmt_no_short_if { -> statement } =
      {up} for assignment to expression do stmt_no_short_if { -> New statement.for_up(assignment.statement, expression.expression, [stmt_no_short_if.statement]) } |
      {down} for assignment downto expression do stmt_no_short_if { -> New statement.for_down(assignment.statement, expression.expression, [stmt_no_short_if.statement]) };

  procedure_call { -> statement } =
      {with_args} identifier left_paren argument_list? right_paren { -> New statement.proc_call(identifier, [argument_list.expression]) } |
      {no_arg} identifier { -> New statement.proc_call(identifier, []) };

  argument_list { -> expression* } =
      {expression} arg { -> [arg.expression] } |
      {argument_list} argument_list comma arg { -> [argument_list.expression, arg.expression] };

  arg { -> expression } =
      {simple} expression { -> expression.expression } |
      {width} expression colon [width]:number { -> New expression.sized_expr(expression.expression, width) };

  assignment { -> statement } =
      [var]:var_access assign_op [expr]:expression { -> New statement.assign(var.expression, expr.expression) };

  expression { -> expression } =
      conditional_expression { -> conditional_expression.expression };

  conditional_expression { -> expression } =
      {equal_expression} equal_expression { -> equal_expression.expression };

  equal_expression { -> expression } =
      {comparison_expression} comparison_expression { -> comparison_expression.expression } |
      {equal} equal_expression equal comparison_expression { -> New expression.equal(equal_expression.expression, comparison_expression.expression) } |
      {not_equal} equal_expression neq comparison_expression { -> New expression.not_equal(equal_expression.expression, comparison_expression.expression) } |
      {not_equal_diamond} equal_expression neq_diamond comparison_expression { -> New expression.not_equal_diamond(equal_expression.expression, comparison_expression.expression) };

  comparison_expression { -> expression } =
      {add_expression} add_expression { -> add_expression.expression } |
      {lt} comparison_expression lt add_expression { -> New expression.lt(comparison_expression.expression, add_expression.expression) } |
      {gt} comparison_expression gt add_expression { -> New expression.gt(comparison_expression.expression, add_expression.expression) } |
      {leq} comparison_expression leq add_expression { -> New expression.leq(comparison_expression.expression, add_expression.expression) } |
      {geq} comparison_expression geq add_expression { -> New expression.geq(comparison_expression.expression, add_expression.expression) };

  add_expression { -> expression } =
      {mult_expression} mult_expression { -> mult_expression.expression } |
      {plus} add_expression plus mult_expression { -> New expression.add(add_expression.expression, mult_expression.expression) } |
      {minus} add_expression minus mult_expression { -> New expression.sub(add_expression.expression, mult_expression.expression) };

  mult_expression { -> expression } =
      {bool_expression} bool_expression { -> bool_expression.expression } |
      {times} mult_expression times bool_expression { -> New expression.mult(mult_expression.expression, bool_expression.expression) } |
      {division} mult_expression divided bool_expression { -> New expression.div(mult_expression.expression, bool_expression.expression) } |
      {mod} mult_expression mod bool_expression { -> New expression.mod(mult_expression.expression, bool_expression.expression) }  |
      {div} mult_expression div bool_expression { -> New expression.div_div(mult_expression.expression, bool_expression.expression) } ;

  bool_expression { -> expression } =
      {unary_expression} unary_expression { -> unary_expression.expression } |
      {or} bool_expression or unary_expression { -> New expression.or(bool_expression.expression, unary_expression.expression) } |
      {and} bool_expression and unary_expression { -> New expression.and(bool_expression.expression, unary_expression.expression) };

  unary_expression { -> expression } =
      {val} val_expression { -> val_expression.expression } |
      {plus} plus unary_expression { -> New expression.plus(unary_expression.expression) } |
      {minus} minus unary_expression { -> New expression.minus(unary_expression.expression) } |
      {not} not unary_expression { -> New expression.not(unary_expression.expression) };

  val_expression { -> expression } =
      {procedure} procedure_invocation { -> procedure_invocation.expression } |
      {literal} [literal]:literal_value { -> literal.expression } |
      {var} var_access { -> var_access.expression } |
      {expr} left_paren expression right_paren { -> expression.expression };

  var_access { -> expression } =
      {var} identifier { -> New expression.identifier(identifier) } |
      {rec} record_access { -> record_access.expression } |
      {arr} array_access { -> array_access.expression } |
      {pointer} pointer_access { -> pointer_access.expression };

  record_access { -> expression } =
      var_access dot [member]:identifier { -> New expression.record_access(var_access.expression, member) };

  array_access { -> expression } =
      identifier left_square_bracket expression right_square_bracket { -> New expression.array_access(identifier, expression.expression) };

  pointer_access { -> expression } =
      [address]:identifier caret { -> New expression.pointer_access(address) };

  // FIXME: Bridge the gap between procedure_invocation/expression and procedure_call/statement further up.
  procedure_invocation { -> expression } =
      identifier left_paren argument_list? right_paren { -> New expression.proc_call(identifier, [argument_list.expression]) };

  literal_value { -> expression } =
      {num} number { -> New expression.number(number) } |
      {string} string_literal { -> New expression.string(string_literal) } |
      {true} true { -> New expression.true(true) } |
      {false} false { -> New expression.false(false) };

  generic_type { -> typedef } =
      {packed_type} packed generic_type { -> New typedef.packed(generic_type.typedef) } |
      {file_def} file of [sub_type]:simple_type { -> New typedef.file(sub_type.typedef) } |
      {pointer} caret simple_type { -> New typedef.pointer(simple_type.typedef) } |
      {record} record_def { -> record_def.typedef } |
      {simple} [type]:simple_type { -> type.typedef };

  range { -> expression } =
      [min_val]:expression range_op [max_val]:expression { -> New expression.range(min_val, max_val) };

  range_type { -> typedef } =
      [min_val]:expression range_op [max_val]:expression { -> New typedef.range(min_val, max_val) };

  array_type_def { -> typedef } =
      array left_square_bracket simple_type right_square_bracket of generic_type { -> New typedef.array(simple_type.typedef, generic_type.typedef) };

  pointer_def { -> typedef } =
      caret simple_type { -> New typedef.pointer(simple_type.typedef) };

  record_def { -> typedef } =
      record record_member+ end { -> New typedef.record([record_member.statement]) };

  record_member { -> statement } =
      {simple_member} identifier_list colon generic_type semicolon { -> New statement.type_decl([identifier_list.identifier], generic_type.typedef) } |
      {case_member} case identifier of case_branch+ { -> New statement.record_member(identifier, [case_branch.statement]) };

  // FIXME: value is “number” when it should be “expression”
  // But this causes shift/reduce conflict with var_access.
  case_branch { -> statement } =
      {branch} number colon left_paren simple_member_type_list right_paren semicolon { -> New statement.record_member_opt(New expression.number(number), [simple_member_type_list.statement]) };

  simple_member_type_list { -> statement* } =
      {simple} simple_member_type { -> [simple_member_type.statement] } |
      {list} simple_member_type semicolon simple_member_type_list { -> [simple_member_type.statement, simple_member_type_list.statement] };

  simple_member_type { -> statement } =
      identifier_list colon simple_type { -> New statement.var([identifier_list.identifier], simple_type.typedef, Null) };

  simple_type { -> typedef } =
      {char_type} char { -> New typedef.char(char) } |
      {boolean_type} boolean { -> New typedef.bool(boolean) } |
      {integer_type} integer { -> New typedef.int(integer) } |
      {real_type} real { -> New typedef.real(real) } |
      {byte_type} byte { -> New typedef.byte(byte) } |
      {range_type} range_type { -> range_type.typedef } |
      {array_type} array_type_def { -> array_type_def.typedef } |
      {user_type} [type_name]:identifier { -> New typedef.def(type_name) };



Abstract Syntax Tree

  pascal_program =
      program_header
      [declarations]:statement*
      [body]:statement*;

  program_header =
      identifier;

  statement =
      {label} number* |
      {goto} number |
      {block} statement* |
      {proc_call} [name]:identifier expression* |
      {proc_decl} [name]:identifier [args]:statement* [body]:statement* |
      {func_decl} [name]:identifier [return_type]:typedef [args]:statement* [body]:statement* |
      {repeat} [cond]:expression statement* |
      {while} [cond]:expression statement* |
      {if} [cond]:expression [then]:statement* [else]:statement* |
      {for_up} [assign]:statement [value]:expression statement* |
      {for_down} [assign]:statement [value]:expression statement* |
      {const} identifier expression |
      {var} [name]:identifier* [type]:typedef [value]:expression? |
      {type_decl} [name]:identifier* [type]:typedef |
      {record_member} [name]:identifier statement* |
      {record_member_opt} [value]:expression [fields]:statement* |
      {assign} [var]:expression [value]:expression |
      {labelled} number statement* |
      {empty} ;

  typedef =
      {char} char |
      {bool} boolean |
      {int} integer |
      {real} real |
      {byte} byte |
      {array} [range]:typedef [type]:typedef |
      {record} statement* |
      {range} [min]:expression [max]:expression |
      {def} identifier |
      {file} typedef |
      {packed} typedef |
      {pointer} typedef;

  expression =
      {add} [left]:expression [right]:expression |
      {sub} [left]:expression [right]:expression |
      {mult} [left]:expression [right]:expression |
      {div} [left]:expression [right]:expression |
      {mod} [left]:expression [right]:expression |
      {div_div} [left]:expression [right]:expression |
      {equal} [left]:expression [right]:expression |
      {not_equal} [left]:expression [right]:expression |
      {not_equal_diamond} [left]:expression [right]:expression |
      {lt} [left]:expression [right]:expression |
      {gt} [left]:expression [right]:expression |
      {leq} [left]:expression [right]:expression |
      {geq} [left]:expression [right]:expression |
      {or} [left]:expression [right]:expression |
      {and} [left]:expression [right]:expression |
      {number} number |
      {sized_expr} expression number |
      {string} string_literal |
      {float} float |
      {range} [min]:expression [max]:expression |
      {plus} [positive]:expression |
      {minus} [negative]:expression |
      {not} [not]:expression |
      {true} true |
      {identifier} identifier |
      {array_access} [array]:identifier expression |
      {record_access} [record]:expression [field]:identifier |
      {pointer_access} [address]:identifier |
      {proc_call} [name]:identifier [args]:expression* |
      {false} false;
